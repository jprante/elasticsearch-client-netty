buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://xbib.org/repository'
        }
    }
    dependencies {
        classpath "org.xbib.elasticsearch:gradle-plugin-elasticsearch-build:6.2.2.0"
    }
}

apply plugin: 'org.xbib.gradle.plugin.elasticsearch.build'

configurations {
    main
    tests
}

dependencies {
    compile project(':api')
    compile "org.xbib:metrics:${project.property('xbib-metrics.version')}"
    compileOnly "org.apache.logging.log4j:log4j-api:${project.property('log4j.version')}"
    testCompile "org.xbib.elasticsearch:elasticsearch-test-framework:${project.property('xbib-elasticsearch-test.version')}"
    testRuntime "org.xbib.elasticsearch:elasticsearch-test-framework:${project.property('xbib-elasticsearch-test.version')}"
}

jar {
    baseName "${rootProject.name}-common"
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "${project.archivesBaseName}-tests"
    from sourceSets.test.output
}

artifacts {
    main jar
    tests testJar
    archives sourcesJar, javadocJar
}

test {
    enabled = false
    jvmArgs "-javaagent:" + configurations.alpnagent.asPath
    systemProperty 'path.home', project.buildDir.absolutePath
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

randomizedTest {
    enabled = false
}

esTest {
    // test with the jars, not the classes, for security manager
    classpath = files(configurations.testRuntime) + configurations.main.artifacts.files + configurations.tests.artifacts.files
    systemProperty 'tests.security.manager', 'true'
}
esTest.dependsOn jar, testJar

